apply plugin: 'com.android.application'
apply plugin: "kotlin-android"
apply plugin: 'kotlin-android-extensions'
apply plugin: "kotlin-kapt"

android {
    signingConfigs {
        config {
            // 定义在 gradle.properties
            keyAlias KEYSTORE_ALIAS
            keyPassword KEYSTORE_PASSWORD
            storeFile file(KEYSTORE_PATH)
            storePassword KEY_PASSWORD
        }
    }
    compileSdkVersion compileSdkVersionValue
    buildToolsVersion buildToolsVersionValue

    dexOptions {
        incremental true
        javaMaxHeapSize "4g"
    }

    sourceSets {
        main.java.srcDirs += "src/main/java"
        main.java.srcDirs += "src/person/java"
    }
    dataBinding {
        enabled = true
    }
    defaultConfig {
//        useLibrary 'org.apache.http.legacy'
        renderscriptTargetApi 18
        renderscriptSupportModeEnabled true

        multiDexEnabled true
        minSdkVersion 15

        javaCompileOptions {
            annotationProcessorOptions {
                // coding 和 coding 企业版 必须使用相同的包名，否则 androidannotations 生成代码会失败
                arguments = [resourcePackageName : "net.coding.program",
                             "logLevel"          : "TRACE", // 打印更多 log，AA 生成错误会导致几百个编译错误
                             "logAppenderConsole": "true"]
            }
        }
    }
    android {
        lintOptions {
            checkReleaseBuilds false
            abortOnError false
        }
    }
    flavorDimensions "mode", "version"
    productFlavors {

        // 版本号不能放在
        person {
            dimension "mode"
            applicationId "net.coding.program"
            versionCode 510
            versionName "5.1"
            targetSdkVersion 23
        }
        enterprise {
            dimension "mode"
            applicationId "net.coding.program.enterprise"
            versionCode 270
            versionName "2.7"
            targetSdkVersion 21

            // 信鸽依赖
            ndk {
                abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a'
                // 还可以添加 'x86', 'x86_64', 'mips', 'mips64'
            }
            manifestPlaceholders = [
                    XG_ACCESS_ID : "2100249649",
                    XG_ACCESS_KEY: "AE6DQ4C5G35G",
            ]


        }

        dev {
            dimension "version"
            // dev utilizes minSDKVersion = 21 to allow the Android gradle plugin
            // to pre-dex each module and produce an APK that can be tested on
            // Android Lollipop without time consuming dex merging processes.
            minSdkVersion 21


            signingConfig signingConfigs.config

        }

        prod {
            dimension "version"
            // The actual minSdkVersion for the application.
            minSdkVersion 15
        }
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            jniDebuggable false
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxerrs" << "99999" // 显示更多的编译错误，默认只显示 100 个
    }
}

kapt {
    correctErrorTypes = true

    javacOptions {
        option("-Xmaxerrs", 500)
    }
}

dependencies {
    kapt 'com.android.databinding:compiler:3.0.1'

    kapt "org.androidannotations:androidannotations:$AAVersion"
    implementation "org.androidannotations:androidannotations-api:$AAVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"

    implementation project(':push-coding')
    implementation project(':push-xiaomi')
    implementation project(':widget-pick-photo')
    implementation project(':common-coding')
    implementation project(':pay-coding')
    implementation project(':terminal-coding')

    // 信鸽依赖
    //信鸽jar
    compile 'com.tencent.xinge:xinge:3.2.2-release'
    //wup包
    compile 'com.tencent.wup:wup:1.0.0.E-release'
    //mid包
    compile 'com.tencent.mid:mid:4.0.6-release'

    // dagger 2 依赖
    def daggerVersion = '2.10'
    compile "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    compile "com.google.dagger:dagger-android:$daggerVersion"
    compile "com.google.dagger:dagger-android-support:$daggerVersion"

    // if you use the support libraries
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"

    // google 兼容库
    compile "com.android.support:support-v4:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.android.support:percent:$supportVersion"
    compile "com.android.support:cardview-v7:$supportVersion"
    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:support-annotations:$supportVersion"
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.skyfishjy.ripplebackground:library:1.0.1'
    compile 'com.loopj.android:android-async-http:1.4.9'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'se.emilsjolander:stickylistheaders:2.7.0'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.github.rampo.updatechecker:library:2.1.4'
    compile 'com.daimajia.swipelayout:library:1.1.7@aar'
    compile 'com.melnykov:floatingactionbutton:1.3.0'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'pl.droidsonroids.gif:android-gif-drawable:1.1.11'
    compile 'com.ms-square:etsyblur:0.1.0'
    compile 'com.github.lzyzsd:circleprogress:1.1.0@aar'
    compile 'it.neokree:MaterialNavigationDrawer:1.3.2'
    compile 'com.prolificinteractive:parallaxpager:2.2.1'
    compile 'com.davemorrissey.labs:subsampling-scale-image-view:3.5.0'

    // umeng 统计
    compile 'com.umeng.analytics:analytics:6.1.2'

    // 高版本fix使用support库crash
    compile 'uk.co.chrisjenx:calligraphy:2.1.0'
    compile 'com.dlazaro66.qrcodereaderview:qrcodereaderview:1.0.0'
    compile 'org.apmem.tools:layouts:1.9@aar'

    //flow_layout
    compile('cn.trinea.android.view.autoscrollviewpager:android-auto-scroll-view-pager:1.1.2') {
        exclude module: 'support-v4'
    }

    //    compile 'com.twotoasters.jazzylistview:library:1.2.1'
    // 删除旧版 umeng
//    implementation project(':social_sdk_library_project')
    implementation project(':bottom-bar')
    implementation project(':luban')
    compile 'com.bigkoo:convenientbanner:1.0.8'

    // 方便显示 view 显示一些样式，不用写 selector
    compile 'com.flyco.roundview:FlycoRoundView_Lib:1.1.4@aar'

    // 列表控件，
    compile 'com.marshalchen.ultimaterecyclerview:library:0.7.0'

    // 比 ultimaterecyclerview 轻量，用起来比较舒服，实现代码也好些
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.28'
    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.9.3@aar'
    compile 'org.eclipse.jgit:org.eclipse.jgit:3.7.1.201504261725-r'
    compile 'com.orhanobut:logger:1.15'
    compile 'com.github.bmelnychuk:atv:1.2.9'

    // 网络请求
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.5.0'
    compile 'com.google.code.gson:gson:2.8.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'
    compile 'com.squareup.okhttp3:okhttp:3.5.0'
    compile 'io.reactivex:rxjava:1.0.14'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'

    // 仿 apple 的 segmentied control
    compile 'info.hoang8f:android-segmented:1.0.6'

    // 圆形头像
    compile 'com.mikhaellopez:circularimageview:3.0.2'

    // 文件下载
    compile 'com.liulishuo.filedownloader:library:1.4.3'

    // xml 解析
    compile 'org.jsoup:jsoup:1.10.3'

    // 常用工具类
    compile 'com.blankj:utilcode:1.9.0'

    // jar 依赖
    compile files('libs/alipaySingle-20170510.jar')
    compile files('libs/BaiduLBS_Android.jar')
    provided files('../common-coding/libs/android-viewbadger.jar')   // 小红点
    provided files('../common-coding/libs/pinyin4j-2.5.0.jar')
    implementation project(':share-coding')
}

// 新功能提示，使用弹出框的样式 start ----------------------------------------------------------------
repositories {
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    compile 'com.github.cpiz:BubbleView:1.0.2'
}
// 新功能提示，使用弹出框的样式 end ----------------------------------------------------------------

repositories {
    flatDir {
        dirs 'libs'
    }
}
dependencies {
    compile(name: 'checknetwork', ext: 'aar')
}

// 高斯模糊 start --------------------------------------------------------------------------
repositories {
    maven { url 'https://github.com/500px/500px-android-blur/raw/master/releases/' }
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}

dependencies {
    compile 'com.fivehundredpx:blurringview:1.0.0'
}
// 高斯模糊 end   --------------------------------------------------------------------------
dependencies {
    implementation 'com.android.support.constraint:constraint-layout:+'
}
