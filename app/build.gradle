apply plugin: 'com.android.application'
apply plugin: "kotlin-android"
apply plugin: 'kotlin-android-extensions'
apply plugin: "kotlin-kapt"

android {
    signingConfigs {
        config {
            // 定义在 gradle.properties
            keyAlias KEYSTORE_ALIAS
            keyPassword KEYSTORE_PASSWORD
            storeFile file(KEYSTORE_PATH)
            storePassword KEY_PASSWORD
        }
    }
    compileSdkVersion compileSdkVersionValue
    buildToolsVersion buildToolsVersionValue

    dexOptions {
        javaMaxHeapSize "4g"
    }

    sourceSets {
        main.java.srcDirs += "src/main/java"
        main.java.srcDirs += "src/person/java"
    }
    dataBinding {
        enabled = true
    }
    defaultConfig {
//        useLibrary 'org.apache.http.legacy'
        renderscriptTargetApi 18
        renderscriptSupportModeEnabled true

        multiDexEnabled true
        minSdkVersion 15

        javaCompileOptions {
            annotationProcessorOptions {
                // coding 和 coding 企业版 必须使用相同的包名，否则 androidannotations 生成代码会失败
                arguments = [resourcePackageName : "net.coding.program",
                             "logLevel"          : "TRACE", // 打印更多 log，AA 生成错误会导致几百个编译错误
                             "logAppenderConsole": "true"]
            }
        }
    }
    android {
        lintOptions {
            checkReleaseBuilds false
            abortOnError false
        }
    }
    flavorDimensions "mode"
    productFlavors {

        // 版本号不能放在
        person {
            dimension "mode"
            applicationId "net.coding.program"
            versionCode 540
            versionName "5.4"
            targetSdkVersion 23

            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
            signingConfig signingConfigs.config
        }
        enterprise {
            dimension "mode"
            applicationId "net.coding.program.enterprise"
            versionCode 295
            versionName "2.9.5"
            targetSdkVersion 21

            // 信鸽依赖
            ndk {
                abiFilters 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'x86_64', 'x86'
                // 还可以添加 , 'x86_64', 'mips', 'mips64'
            }

            manifestPlaceholders = [
                    XG_ACCESS_ID : "2100249649",
                    XG_ACCESS_KEY: "AE6DQ4C5G35G",
            ]
            signingConfig signingConfigs.config
        }
    }
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.config
        }
        debug {
            signingConfig signingConfigs.config
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxerrs" << "99999" // 显示更多的编译错误，默认只显示 100 个
    }
}

kapt {
    correctErrorTypes = true

    javacOptions {
        option("-Xmaxerrs", 500)
    }
}

dependencies {
    kapt "org.androidannotations:androidannotations:$AAVersion"

    implementation project(':common-third-library')
    implementation project(':push-coding')
    implementation project(':push-xiaomi')
    implementation project(':widget-pick-photo')
    implementation project(':common-coding')
    implementation project(':pay-coding')
    implementation project(':terminal-coding')

    // 信鸽依赖, 只有企业版用了信鸽
    if (DEV_ENTERPRISE.toBoolean()) {
        //信鸽jar
        implementation 'com.tencent.xinge:xinge:3.2.2-release'
        //wup包
        implementation 'com.tencent.wup:wup:1.0.0.E-release'
        //mid包
        implementation 'com.tencent.mid:mid:4.0.6-release'
    }

    // dagger 2 依赖
    def daggerVersion = '2.10'
    implementation "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"

    // if you use the support libraries
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"

    // google 兼容库
    implementation "com.android.support:support-v4:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:percent:$supportVersion"
    implementation "com.android.support:cardview-v7:$supportVersion"
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'com.skyfishjy.ripplebackground:library:1.0.1'
    implementation 'com.loopj.android:android-async-http:1.4.9'
    implementation 'com.astuetz:pagerslidingtabstrip:1.0.1'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'se.emilsjolander:stickylistheaders:2.7.0'
    implementation 'de.hdodenhof:circleimageview:2.1.0'
    implementation 'com.github.rampo.updatechecker:library:2.1.4'
    implementation 'com.daimajia.swipelayout:library:1.1.7@aar'
    implementation 'com.melnykov:floatingactionbutton:1.3.0'
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.1.11'
    implementation 'com.ms-square:etsyblur:0.1.0'
    implementation 'com.github.lzyzsd:circleprogress:1.1.0@aar'
    implementation 'it.neokree:MaterialNavigationDrawer:1.3.2'
    implementation 'com.prolificinteractive:parallaxpager:2.2.1'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.5.0'
    configurations {
        all*.exclude group: 'com.android.support', module: 'support-v13'
    }

    // umeng 统计
    implementation 'com.umeng.analytics:analytics:6.1.2'

    // 高版本fix使用support库crash
    implementation 'uk.co.chrisjenx:calligraphy:2.1.0'
    implementation 'com.dlazaro66.qrcodereaderview:qrcodereaderview:1.0.0'
    implementation 'org.apmem.tools:layouts:1.9@aar'

    //flow_layout
    compile('cn.trinea.android.view.autoscrollviewpager:android-auto-scroll-view-pager:1.1.2') {
        exclude module: 'support-v4'
    }

    //    implementation 'com.twotoasters.jazzylistview:library:1.2.1'

    // 删除旧版 umeng

    //    implementation project(':social_sdk_library_project')
    implementation project(':bottom-bar')
    implementation project(':luban')
    implementation 'com.bigkoo:convenientbanner:1.0.8'

    // 方便显示 view 显示一些样式，不用写 selector
    implementation 'com.flyco.roundview:FlycoRoundView_Lib:1.1.4@aar'

    // 列表控件，
    implementation 'com.marshalchen.ultimaterecyclerview:library:0.7.3'

    // 比 ultimaterecyclerview 轻量，用起来比较舒服，实现代码也好些
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.34'
    implementation 'com.tbruyelle.rxpermissions:rxpermissions:0.9.3@aar'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:3.7.1.201504261725-r'
    implementation 'com.orhanobut:logger:1.15'
    implementation 'com.github.bmelnychuk:atv:1.2.9'

    // 网络请求
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    implementation 'com.squareup.okhttp3:okhttp:3.9.1'
    implementation 'io.reactivex:rxjava:1.2.4'
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'

    // 仿 apple 的 segmentied control
    implementation 'info.hoang8f:android-segmented:1.0.6'

    // 圆形头像
    implementation 'com.mikhaellopez:circularimageview:3.0.2'

    // 文件下载
    implementation 'com.liulishuo.filedownloader:library:1.4.3'

    // xml 解析
    implementation 'org.jsoup:jsoup:1.10.3'

    // 常用工具类
    implementation 'com.blankj:utilcode:1.9.0'

    // git 库
    implementation 'org.eclipse.jgit:org.eclipse.jgit:3.7.1.201504261725-r'

    // jar 依赖
    implementation files('libs/alipaySingle-20170510.jar')
    implementation files('libs/BaiduLBS_Android.jar')
    provided files('../common-coding/libs/android-viewbadger.jar')

    // 小红点
    provided files('../common-coding/libs/pinyin4j-2.5.0.jar')
    implementation project(':share-coding')
    implementation project(':git-code-coding')

    // 测试框架
    implementation "com.android.support:support-annotations:$supportVersion"
    androidTestImplementation "com.android.support.test:runner:$runnerVersion"
    androidTestImplementation "com.android.support.test:rules:$rulesVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"
}

// 新功能提示，使用弹出框的样式 start ----------------------------------------------------------------
repositories {
    jcenter()
    maven {
        url "https://jitpack.io"
    }
}

dependencies {
    implementation 'com.github.cpiz:BubbleView:1.0.2'

    // 图片裁剪，系统的坑太多
    implementation 'com.github.yalantis:ucrop:2.2.2'
}

// 新功能提示，使用弹出框的样式 end ----------------------------------------------------------------
repositories {
    flatDir {
        dirs 'libs'
    }
}
dependencies {
    compile(name: 'checknetwork', ext: 'aar')
}

// 高斯模糊 start --------------------------------------------------------------------------
repositories {
    maven { url 'https://github.com/500px/500px-android-blur/raw/master/releases/' }
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}

dependencies {
    implementation 'com.fivehundredpx:blurringview:1.0.0'
}
// 高斯模糊 end   --------------------------------------------------------------------------
